.include "/home/marik/Project/m32Adef.inc"
; Internal Hardware Init  ======================================
		.equ 	XTAL = 16000000 	
		.equ 	baudrate = 9600  
		.equ 	bauddivider = XTAL/(16*baudrate)-1

.cseg
.org 0
	ldi R16,low(RAMEND) ;инициализация стека
	out SPL,R16
	ldi R16,high(RAMEND)
	out SPH, R16

		LDI 	R16, low(bauddivider)
		OUT 	UBRRL,R16
		LDI 	R16, high(bauddivider)
		OUT 	UBRRH,R16
 
		LDI 	R16,0
		OUT 	UCSRA, R16
 
; Прерывания запрещены, прием-передача разрешен.
		LDI 	R16, (1<<RXEN)|(1<<TXEN)|(0<<RXCIE)|(0<<TXCIE)|(0<<UDRIE)
		OUT 	UCSRB, R16	
 
; Формат кадра - 8 бит, пишем в регистр UCSRC, за это отвечает бит селектор
		LDI 	R16, (1<<URSEL)|(1<<UCSZ0)|(1<<UCSZ1)
		OUT 	UCSRC, R16

	LDI R16,128	;НАстройка TWI
	OUT TWBR, R16
	
ReadTime:	LDI R16, 1<<TWEN|1<<TWSTA|1<<TWINT	;START
	OUT TWCR, R16

	rcall wait1	;Ждем прерывания
	IN R16, TWSR
	ANDI R16, 0XF8
	CPI R16, 0X08	;ПРОВЕРКА СТАРТА
	BRNE ERROR2

	LDI R16,0b11010000	;АДРЕС УСТРОЙСТВА
	OUT TWDR, R16
	LDI R16, 1<<TWINT|1<<TWEN
	OUT TWCR, R16

	rcall wait1	;Ждем прерывания
	IN R16, TWSR
	ANDI R16, 0XF8
	CPI R16, 0X18	;ПРОВЕРКА ОТВЕТА НА АДРЕС
	BRNE ERROR2

	LDI R16,0X00	;АДРЕС СЛОВА
	OUT TWDR, R16
	LDI R16, 1<<TWINT|1<<TWEN
	OUT TWCR, R16

	rcall wait1	;Ждем прерывания
	IN R16, TWSR
	ANDI R16, 0XF8
	CPI R16, 0X28	;ПРОВЕРКА ПРИНЯТИЯ БАЙТА
	BRNE ERROR2
	rjmp Next

ERROR2: rjmp ERROR




Next:	LDI R16, 1<<TWEN|1<<TWSTA|1<<TWINT	;RESTART
	OUT TWCR, R16


	rcall wait1	;Ждем прерывания
	IN R16, TWSR
	ANDI R16, 0XF8
	CPI R16, 0X10	;ПРОВЕРКА РЕСТАРТА
	BRNE ERROR

	LDI R16,0b11010000|1<<0	;АДРЕС УСТРОЙСТВА
	OUT TWDR, R16
	LDI R16, 1<<TWINT|1<<TWEN
	OUT TWCR, R16

	rcall wait1	;Ждем прерывания
	IN R16, TWSR
	ANDI R16, 0XF8
	CPI R16, 0X40	;ПРОВЕРКА ОТВЕТА НА АДРЕС R
	BRNE ERROR

	LDI R16, 1<<TWINT|1<<TWEN|1<<TWEA	;Бум читать
	OUT TWCR, R16

	rcall wait1	;Ждем прерывания
	IN R16, TWSR
	ANDI R16, 0XF8
	CPI R16, 0X50	;ПРОВЕРКА ЧТЕНИЯ
	BRNE ERROR
	IN R16, TWDR
	MOV R10,R16	;Записали секунды в R10

	LDI R16, 1<<TWINT|1<<TWEN|1<<TWEA	;Бум читать
	OUT TWCR, R16

	rcall wait1	;Ждем прерывания
	IN R16, TWSR
	ANDI R16, 0XF8
	CPI R16, 0X50	;ПРОВЕРКА ЧТЕНИЯ
	BRNE ERROR
	IN R16, TWDR
	MOV R11,R16	;Записали минуты в R11

	
	LDI R16, 1<<TWINT|1<<TWEN|0<<TWEA	;Бум читать
	OUT TWCR, R16

	rcall wait1	;Ждем прерывания
	IN R16, TWSR
	ANDI R16, 0XF8
	CPI R16, 0X58	;ПРОВЕРКА ЧТЕНИЯ
	BRNE ERROR
	IN R16, TWDR
	MOV R12,R16	;Записали Часы в R12
	
	LDI R16, 1<<TWINT|1<<TWEN|1<<TWSTO	;STOP
	OUT TWCR, R16

rjmp Begin

ERROR: LDI R16, 0XAA
	rcall uart_snt
RJMP ReadTime




Begin: rcall TimeToASCII
	ldi ZL,Low(TimeInASCII)
	ldi ZH,High(TimeInASCII)  ;загрузка начального адреса массива
	LDI R18,8


Risuy:	LD	R16, Z+
	rcall uart_snt
	DEC R18
	TST R18
	brne Risuy
	LDI R16, 0x0A	;На новую строку
	rcall uart_snt
	LDI R16, 0x0D	;в начало строки
	rcall uart_snt

RJMP ReadTime


END: nop
rjmp END



wait1: IN R16, TWCR		;Ждем прерывания
	SBRS R16, TWINT
	RJMP wait1
	RET



; Процедура отправки байта
uart_snt:	SBIS 	UCSRA,UDRE	; Пропуск если нет флага готовности
		RJMP	uart_snt 	; ждем готовности - флага UDRE
 
		OUT	UDR, R16	; шлем байт
		RET







;|---------------------------------------------------------------------------
;| Процедура преобразования времени в ASCII
;| На входе: секунды в R10
;|            минуты в R11
;|              часы в R12
;| На выходе: В TimeInASCII и TimeInASCII+1 - Часы,
;| В TimeInASCII+3 и TimeInASCII+4 - Минуты,
;| В TimeInASCII+6 и TimeInASCII+7 - Секунды,
;| В TimeInASCII+2 и TimeInASCII+5 - Двоеточие
;|---------------------------------------------------------------------------
TimeToASCII:	PUSH R16
	
	MOV R16,R12
	LSR R16
	LSR R16
	LSR R16
	LSR R16
	SUBI R16, (-48)	; Прибавляем 48 для получения символа
	STS TimeInASCII, R16

	MOV R16,R12
	ANDI R16,0x0F
	SUBI R16, (-48)	; Прибавляем 48 для получения символа
	STS TimeInASCII+1, R16

	MOV R16,R11
	LSR R16
	LSR R16
	LSR R16
	LSR R16
	SUBI R16, (-48)	; Прибавляем 48 для получения символа
	STS TimeInASCII+3, R16

	MOV R16,R11
	ANDI R16,0x0F
	SUBI R16, (-48)	; Прибавляем 48 для получения символа
	STS TimeInASCII+4, R16

	MOV R16,R10
	LSR R16
	LSR R16
	LSR R16
	LSR R16
	SUBI R16, (-48)	; Прибавляем 48 для получения символа
	STS TimeInASCII+6, R16

	MOV R16,R10
	ANDI R16,0x0F
	SUBI R16, (-48)	; Прибавляем 48 для получения символа
	STS TimeInASCII+7, R16

	LDI R16, 0x3A
	STS TimeInASCII+2, R16
	STS TimeInASCII+5, R16

	POP R16
RET
;|---------------------------------------------------------------------------
;|                               END
:|---------------------------------------------------------------------------






.DSEG
TimeInASCII:	.byte	8

